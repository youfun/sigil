defmodule Sigil.ConversationTranscriptStoreTest do
  use ExUnit.Case, async: false

  alias Sigil.ConversationTranscriptStore

  setup do
    old_home = System.get_env("HOME")

    home_dir =
      Path.join(
        System.tmp_dir!(),
        "sigil_conversation_transcript_home_#{System.unique_integer([:positive])}"
      )

    System.put_env("HOME", home_dir)

    on_exit(fn ->
      if old_home, do: System.put_env("HOME", old_home), else: System.delete_env("HOME")
      if File.exists?(home_dir), do: File.rm_rf!(home_dir)
    end)

    :ok
  end

  test "append, list, and update transcript entries" do
    {:ok, conversation} = Sigil.ConversationStore.create("default", id: "transcript-store")
    conversation_id = conversation["id"]

    assert {:ok, user} =
             ConversationTranscriptStore.append(conversation_id, %{
               "id" => "msg-user-1",
               "role" => "user",
               "content" => "hello",
               "direction" => "inbound",
               "channel" => "sns"
             })

    assert user["sequence"] == 1

    assert {:ok, assistant} =
             ConversationTranscriptStore.append(conversation_id, %{
               "id" => "msg-assistant-1",
               "role" => "assistant",
               "content" => "hi",
               "direction" => "outbound",
               "channel" => "sns"
             })

    assert assistant["sequence"] == 2

    assert {:ok, updated} =
             ConversationTranscriptStore.update(conversation_id, "msg-assistant-1", %{
               "content" => %{"$append" => " there"},
               "status" => "streaming"
             })

    assert updated["content"] == "hi there"

    assert {:ok, entries} = ConversationTranscriptStore.list(conversation_id)
    assert Enum.map(entries, & &1["id"]) == ["msg-user-1", "msg-assistant-1"]
  end

  test "serializes concurrent appends and updates without losing entries" do
    {:ok, conversation} = Sigil.ConversationStore.create("default", id: "transcript-concurrent")
    conversation_id = conversation["id"]

    assert {:ok, _entry} =
             ConversationTranscriptStore.append(conversation_id, %{
               "id" => "streamed",
               "role" => "assistant",
               "content" => ""
             })

    parent = self()

    tasks =
      for index <- 1..40 do
        Task.async(fn ->
          send(parent, {:ready, self()})

          receive do
            :go ->
              if rem(index, 2) == 0 do
                ConversationTranscriptStore.append(conversation_id, %{
                  "id" => "appended-#{index}",
                  "role" => "user",
                  "content" => Integer.to_string(index)
                })
              else
                ConversationTranscriptStore.update(conversation_id, "streamed", %{
                  "content" => %{"$append" => "x"}
                })
              end
          end
        end)
      end

    Enum.each(tasks, fn task ->
      assert_receive {:ready, task_pid} when task_pid == task.pid
    end)

    Enum.each(tasks, &send(&1.pid, :go))
    Enum.each(tasks, &Task.await(&1, 5_000))

    assert {:ok, entries} = ConversationTranscriptStore.list(conversation_id)
    assert length(entries) == 21
    assert Enum.find(entries, &(&1["id"] == "streamed"))["content"] == String.duplicate("x", 20)
  end
end
