defmodule SigilProbe.NativeTimelineTest do
  use ExUnit.Case, async: true
  use Gettext, backend: SigilProbe.Gettext

  alias SigilProbe.NativeTimeline

  @tiny_png <<137, 80, 78, 71, 13, 10, 26, 10, 0, 0, 0, 13, 73, 72, 68, 82, 0, 0, 0, 1, 0, 0, 0,
              1, 8, 6, 0, 0, 0, 31, 21, 196, 137, 0, 0, 0, 13, 73, 68, 65, 84, 120, 156, 99, 248,
              207, 192, 240, 31, 0, 5, 0, 1, 253, 46, 43, 34, 0, 0, 0, 0, 73, 69, 78, 68, 174, 66,
              96, 130>>

  test "user text and attachment names each align to the trailing edge" do
    for {body, attachments, count} <- [
          {"识别图片", [%{"filename" => "photo.jpg"}], 2},
          {"纯文本消息", [], 1},
          {"", [%{"filename" => "photo.jpg"}], 1}
        ] do
      chat = %{
        entries: [
          %{"id" => "user-1", "role" => "user", "content" => body, "attachments" => attachments}
        ],
        stream: "",
        running: false
      }

      [row | _] = render(chat, MapSet.new())
      [_, column] = row.children
      assert column.props.weight == 3
      assert length(column.children) == count

      for box <- column.children do
        assert box.type == :box
        assert box.props.fill_width
        assert box.props.align == "top_trailing"
      end
    end
  end

  test "sent images sit above the bubble, right aligned, without network or bytes" do
    {chat, dest, relative} = chat_with_png("user-img", "shot.png")

    [row | _] = render(chat, MapSet.new())
    [_, column] = row.children
    [thumbs, bubble] = column.children
    assert thumbs.props.align == "top_trailing"
    assert bubble.props.align == "top_trailing"
    assert hd(bubble.children).props.text == "识别图片"

    images = match_image_nodes(thumbs)
    assert length(images) == 1
    [thumb] = images
    assert thumb.props.src == dest
    assert thumb.props.upload_only == true
    assert thumb.props.local_only == true
    assert thumb.props.width == 40
    encoded = inspect(row)
    refute encoded =~ "https://"
    refute encoded =~ Base.encode64(@tiny_png)
    refute encoded =~ "data:image"
    assert thumb.props.src == Path.join([chat.workspace_path, relative])
  end

  test "write and browser tool cards expose system open and share actions" do
    chat = %{
      conversation: %{"id" => "c1"},
      workspace_id: "ws-1",
      workspace_path: "/tmp/ws",
      entries: [
        %{
          "id" => "t1",
          "content_type" => "tool",
          "tool_name" => "write",
          "tool_status" => "done",
          "input" => %{"file_path" => "out/report.pdf"},
          "content" => "wrote"
        },
        %{
          "id" => "t2",
          "content_type" => "tool",
          "tool_name" => "browser",
          "tool_status" => "done",
          "input" => %{"action" => "open", "url" => "https://example.com/login"},
          "content" => "opened"
        }
      ],
      stream: "",
      running: false
    }

    encoded = inspect(render(chat, MapSet.new(["t1"])), limit: :infinity)
    assert encoded =~ gettext("Open")
    assert encoded =~ gettext("Open in another app")
    assert encoded =~ gettext("Share file")
    assert encoded =~ gettext("Open in system browser")
    assert encoded =~ ":workspace_open"
    assert encoded =~ "https://example.com/login"
  end

  test "write card uses the current workspace for absolute paths" do
    ws = "/tmp/ws-abs"

    chat = %{
      conversation: %{"id" => "c1"},
      workspace_id: "ws-abs",
      workspace_path: ws,
      entries: [
        %{
          "id" => "t1",
          "content_type" => "tool",
          "tool_name" => "write",
          "tool_status" => "done",
          "input" => %{"file_path" => Path.join(ws, "out/report.pdf")},
          "content" => "wrote"
        }
      ],
      stream: "",
      running: false
    }

    tree = render(chat, MapSet.new(["t1"]))
    encoded = inspect(tree)
    assert encoded =~ gettext("Open in another app")
    assert encoded =~ "out/report.pdf"
  end

  test "multiple sent images stay adjacent and cap at four" do
    workspace = tmp_ws()
    conv_id = Ecto.UUID.generate()

    attachments =
      for i <- 1..5 do
        {_dest, relative} = place_png(workspace, conv_id, "img-#{i}")

        %{
          "id" => "img-#{i}",
          "kind" => "image",
          "mime_type" => "image/png",
          "filename" => "p#{i}.png",
          "relative_path" => relative
        }
      end

    chat = chat(workspace, conv_id, "user-m", "多图", attachments)
    [row | _] = render(chat, MapSet.new())
    [_, column] = row.children
    [thumbs, _bubble] = column.children
    images = match_image_nodes(thumbs)
    assert length(images) == 4
    assert length(thumbs.children) == 1
    assert hd(thumbs.children).type == :row
  end

  test "text-only attachments stay names and history restore uses upload refs" do
    workspace = tmp_ws()
    conv_id = Ecto.UUID.generate()
    {_dest, relative} = place_png(workspace, conv_id, "hist-1")

    entry = %{
      "id" => "hist-msg",
      "role" => "user",
      "content" => "历史图",
      "attachments" => [
        %{
          "id" => "hist-1",
          "kind" => "image",
          "mime_type" => "image/png",
          "filename" => "hist.png",
          "relative_path" => relative
        }
      ]
    }

    chat = %{
      conversation: %{"id" => conv_id, "workspace_id" => "ws-hist"},
      workspace_path: workspace,
      workspace_id: "ws-hist",
      entries: [entry],
      stream: "",
      running: false
    }

    tree = render(chat, MapSet.new())
    images = match_image_nodes(hd(tree))
    assert length(images) == 1
    assert hd(images).props.src =~ "/.sigil/uploads/#{conv_id}/"

    text_only = %{
      entries: [
        %{
          "id" => "u-text",
          "role" => "user",
          "content" => "看文件",
          "attachments" => [
            %{
              "id" => "n1",
              "filename" => "note.ex",
              "mime_type" => "text/x-source",
              "kind" => "text"
            }
          ]
        }
      ],
      stream: "",
      running: false
    }

    [row | _] = render(text_only, MapSet.new())
    encoded = inspect(row)
    assert encoded =~ "note.ex"
    assert match_image_nodes(row) == []
  end

  test "missing or naked paths fall back and never use a foreign workspace" do
    workspace = tmp_ws()
    other = tmp_ws()
    conv_id = Ecto.UUID.generate()
    {_dest, relative} = place_png(workspace, conv_id, "own-1")

    owned = %{
      "id" => "own-1",
      "kind" => "image",
      "mime_type" => "image/png",
      "filename" => "own.png",
      "relative_path" => relative,
      "controlled_path" => "/tmp/evil.png"
    }

    missing = %{
      "id" => "gone",
      "kind" => "image",
      "mime_type" => "image/png",
      "filename" => "gone.png",
      "relative_path" => Path.join([".sigil", "uploads", conv_id, "gone.png"])
    }

    chat = chat(workspace, conv_id, "user-own", "图", [owned, missing])
    [row | _] = render(chat, MapSet.new())
    images = match_image_nodes(row)
    assert length(images) == 2
    assert Enum.at(images, 0).props.src == Path.join(workspace, relative)
    refute Enum.at(images, 0).props.src == "/tmp/evil.png"
    assert Enum.at(images, 1).props.src == ""
    assert Enum.at(images, 1).props.fallback == gettext("Image cannot be displayed")

    foreign = %{chat | workspace_path: other}
    [foreign_row | _] = render(foreign, MapSet.new())
    foreign_images = match_image_nodes(foreign_row)
    assert Enum.all?(foreign_images, &(&1.props.src == ""))
  end

  test "sent card resolves the same upload ref by message and attachment id" do
    {chat, dest, _relative} = chat_with_png("user-card", "card.png")

    card =
      render_sent_card(chat, %{message_id: "user-card", attachment_id: "att-1"})

    assert card
    encoded = inspect(card)
    assert encoded =~ "card.png"
    assert encoded =~ "image/png"
    [image] = match_image_nodes(card)
    assert image.props.src == dest
    assert image.props.upload_only == true
    assert image.props.width == 168
    refute render_sent_card(chat, nil)
  end

  # Sent-image paths are resolved when entries enter chat state; render itself
  # never touches the filesystem.
  defp render(chat, toggles),
    do: NativeTimeline.render(SigilProbe.NativeLocalImage.resolve_sent(chat), toggles)

  defp render_sent_card(chat, open),
    do: NativeTimeline.render_sent_card(SigilProbe.NativeLocalImage.resolve_sent(chat), open)

  defp chat_with_png(message_id, name) do
    workspace = tmp_ws()
    conv_id = Ecto.UUID.generate()
    {dest, relative} = place_png(workspace, conv_id, "att-1")

    att = %{
      "id" => "att-1",
      "kind" => "image",
      "mime_type" => "image/png",
      "filename" => name,
      "relative_path" => relative
    }

    {chat(workspace, conv_id, message_id, "识别图片", [att]), dest, relative}
  end

  defp chat(workspace, conv_id, message_id, body, attachments) do
    %{
      conversation: %{"id" => conv_id, "workspace_id" => "ws-bound"},
      workspace_path: workspace,
      workspace_id: "ws-bound",
      entries: [
        %{"id" => message_id, "role" => "user", "content" => body, "attachments" => attachments}
      ],
      stream: "",
      running: false
    }
  end

  defp place_png(workspace, conv_id, att_id) do
    dest = Path.join(Sigil.Uploads.ensure_conversation_dir!(workspace, conv_id), "#{att_id}.png")
    File.write!(dest, @tiny_png)
    {dest, Path.join([".sigil", "uploads", conv_id, "#{att_id}.png"])}
  end

  defp tmp_ws do
    dir = Path.join(System.tmp_dir!(), "tl_ws_#{System.unique_integer([:positive])}")
    File.mkdir_p!(dir)
    dir
  end

  defp match_image_nodes(node), do: collect_images(node, []) |> Enum.reverse()

  defp collect_images(%{type: :image} = node, acc), do: [node | acc]

  defp collect_images(%{children: children}, acc) when is_list(children) do
    Enum.reduce(children, acc, &collect_images/2)
  end

  defp collect_images(_, acc), do: acc
end
